apply plugin: 'com.android.library'

def distDir = project.findProperty("distDir") ?: ""
def jniLibsDir = project.findProperty("jniLibsDir") ?: ""
def revision = project.findProperty("revision") ?: "".replaceAll("\\s", "")

if (!distDir) throw new RuntimeException("expecting --project-prop distDir=??? but was empty")
if (!jniLibsDir) throw new RuntimeException("expecting --project-prop jniLibsDir=??? but was empty")
if (!revision) throw new RuntimeException("expecting --project-prop revision=??? but was empty")

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["${jniLibsDir}"]
        }
    }
}

dependencies {}

apply plugin: 'maven'

task createAAR(type: Upload) {
    project.group = "org.webkit"
    project.version = "r${revision}"

    configuration = configurations.archives
    repositories.mavenDeployer {
        repository url: "file://${distDir}"
        pom.project {
            name "android-jsc"
            artifactId "android-jsc-cppruntime"
            packaging "aar"
        }
    }
}
